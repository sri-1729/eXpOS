alias func R1;
alias pid R2;

if(func == FREE_USER_AREA_PAGE)then

alias upa R3;
upa = [PROCESS_TABLE + (pid * 16) + 11];
multipush(R1, R2, R3);
R1 = RELEASE_PAGE;
R2 = upa;
call MOD_2;
multipop(R1, R2, R3);

return;
endif;



if(func == EXIT_PROCESS)then

multipush(R1, R2);
R1 = FREE_PAGE_TABLE;
R2 = pid;
call MOD_1;
multipop(R1, R2);

multipush(R1, R2);
R1 = FREE_USER_AREA_PAGE;
R2 = pid;
call MOD_1;
multipop(R1, R2);

[PROCESS_TABLE + (pid * 16) + 4] = TERMINATED;

return;
endif;


if(func == FREE_PAGE_TABLE)then

alias pt R3;
pt = [PROCESS_TABLE + (pid * 16) + 14];
[pt + 0] = -1;
[pt + 1] = "0000";
[pt + 2] = -1;
[pt + 3] = "0000";

alias iter R4;
iter = 2;
while(iter < 10)do
	if([pt + (iter * 2)] != -1)then
		multipush(R1, R2, R3, R4);
		R1 = RELEASE_PAGE;
		R2 = [pt + (iter * 2)];
		call MOD_2;
		multipop(R1, R2, R3, R4);
	endif;
	[pt + (iter * 2)] = -1;
	[pt + (iter * 2) + 1] = "0000";
	iter = iter + 1;
endwhile;
return;
endif;



