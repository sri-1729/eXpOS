alias userSP R0;
userSP = SP;
alias currId R5;
currId = [SYSTEM_STATUS_TABLE + 1];

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 9;
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11]*512 - 1;

alias location R1;
alias offset R2;
alias fileName R3;
location = [PTBR + 2 * ((userSP - 4) / 512)];

offset = (userSP - 4) % 512;
fileName = [(location * 512) + offset];

//search in inode table
alias iter R4;
iter = 0;
//print fileName;
while(iter < MAX_FILE_NUM)do
	if([INODE_TABLE + (iter * 16) + 1] == fileName)then
		break;
	endif;
	iter = iter + 1;
endwhile;

//print iter;

if(iter == MAX_FILE_NUM || [INODE_TABLE + (iter * 16)] != EXEC)then
	[([PTBR + 2 * ((userSP - 1) / 512)]) * 512 + ((userSP - 1) % 512)] = -1;
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;
	SP = userSP;
	ireturn;
endif;

multipush(R0, R1, R2, R3, R4, R5);
R1 = EXIT_PROCESS;
R2 = currId;
call MOD_1;
multipop(R0, R1, R2, R3, R4, R5);

R0 = [PROCESS_TABLE + (currId * 16) + 11];
[MEMORY_FREE_LIST + R0] = 1;

[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;//memory free count

SP = R0*512 - 1;

[PROCESS_TABLE + (currId * 16) + 4] = RUNNING;
[PROCESS_TABLE + (currId * 16) + 7] = iter;

[PTBR + 0] = 63;
[PTBR + 1] = "0100";
[PTBR + 2] = 64;
[PTBR + 3] = "0100";

//invalidate heap pages
[PTBR + 4] = -1;
[PTBR + 5] = "0000";

[PTBR + 6] = -1;
[PTBR + 7] = "0000";

//get stack page

multipush(R0, R1, R2, R3, R4, R5);
R1 = GET_FREE_PAGE;
call MOD_2;
R6 = R0;
multipop(R0, R1, R2, R3, R4, R5);
[PTBR + 16] = R6;
[PTBR + 17] = "0110";

multipush(R0, R1, R2, R3, R4, R5);
R1 = GET_FREE_PAGE;
call MOD_2;
R6 = R0;
multipop(R0, R1, R2, R3, R4, R5);
[PTBR + 18] = R6;
[PTBR + 19] = "0110";

R7 = 0;
while(R7 < 4)do
	if([INODE_TABLE + (iter * 16) + 8 + R7] != -1 && R7 < 1)then
		multipush(R0, R1, R2, R3, R4, R5, R7);
		R1 = GET_CODE_PAGE;
		R2 = [INODE_TABLE + (iter * 16) + 8 + R7];
		call MOD_2;
		R6 = R0;
		multipop(R0, R1, R2, R3, R4, R5, R7);
		[PTBR + (R7 + 4) * 2] = R6;
		[PTBR + (R7 + 4)*2 + 1] = "0100";
	else
		[PTBR + (R7 + 4) * 2] = -1;
		[PTBR + (R7 + 4)*2 + 1] = "0000";
	endif;
	R7 = R7 + 1;
endwhile;

R7 = 2;
while(R7 < 10)do
	[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + R7] = -1;
	R7 = R7 + 1;
endwhile;

R7 = 496;
R8 = [PROCESS_TABLE + (currId * 16) + 11];
while(R7 < 512)do
	[R8 + R7] = -1;
	R7 = R7 + 1;
endwhile;
	
[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 4] = [INODE_TABLE + (iter * 16) + 8];
[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 5] = [INODE_TABLE + (iter * 16) + 9];
[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 6] = [INODE_TABLE + (iter * 16) + 10];
[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 7] = [INODE_TABLE + (iter * 16) + 11];
[[PTBR + 16] * 512] = [[PTBR + 8] * 512 + 1];
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]*16) + 9] = 0;
SP = 8*512;
ireturn;


